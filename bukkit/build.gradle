plugins {
    id "java"
    id "com.github.johnrengelman.shadow"
}

repositories {
    flatDir { dirs 'lib' }
    mavenCentral()
}

dependencies {
    implementation project(':core')

    compileOnly 'com.sk89q:worldguard:6.0.0-SNAPSHOT'

    compileOnly 'net.minemora.moraspigot:moraspigot:1.8.8-R0.1-SNAPSHOT'

    compileOnly 'com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT'
    compileOnly 'com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib-API:4.4.0'
    compileOnly('org.inventivetalent:mapmanager:1.7.2-SNAPSHOT') { transitive = false }

    implementation 'com.github.luben:zstd-jni:1.1.1'
    implementation 'co.aikar:fastutil-lite:1.0'

    implementation("org.graalvm.polyglot:polyglot:23.1.2")
    implementation("org.graalvm.polyglot:js-community:23.1.2@pom")

    implementation 'org.primesoft:BlocksHub:2.0'

    testImplementation 'junit:junit:4.13.2'
}

clean {
    delete "../target"
}

tasks.named('jar') {
    archiveFileName = "fawe-bukkit-${rootProject.version}.jar"
    destinationDirectory = file("${rootProject.projectDir}/mvn/com/boydti/fawe-bukkit/${rootProject.version}")
}

shadowJar {
    dependencies {
        include(project(':core'))
        include(dependency('com.github.luben:zstd-jni:1.1.1'))
        include(dependency('co.aikar:fastutil-lite:1.0'))
    }

    archiveFileName = "${rootProject.name}-${project.name}-${rootProject.version}.jar"
    destinationDirectory = file("${rootProject.projectDir}/target")

    relocate('com.google.gson', 'com.sk89q.worldedit.internal.gson')
}


tasks.named('shadowJar').configure {
    doLast {
        ant.checksum file: archiveFile.get().asFile
    }
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}

tasks.register('createPom') {
    doLast {
        // POM para versi√≥n
        def dirVer = file("${rootProject.projectDir}/mvn/com/boydti/fawe-bukkit/${rootProject.version}")
        dirVer.mkdirs()
        def pomVer = """\
            <project>
              <modelVersion>4.0.0</modelVersion>
              <groupId>com.boydti</groupId>
              <artifactId>fawe-bukkit</artifactId>
              <version>${rootProject.version}</version>
              <dependencies/>
            </project>
        """.stripIndent()
        file("${dirVer}/fawe-bukkit-${rootProject.version}.pom").text = pomVer

        def dirLatest = file("${rootProject.projectDir}/mvn/com/boydti/fawe-bukkit/latest")
        dirLatest.mkdirs()
        def pomLatest = """\
            <project>
              <modelVersion>4.0.0</modelVersion>
              <groupId>com.boydti</groupId>
              <artifactId>fawe-bukkit</artifactId>
              <version>latest</version>
              <dependencies/>
            </project>
        """.stripIndent()
        file("${dirLatest}/fawe-bukkit-latest.pom").text = pomLatest
    }
}

tasks.register('copyFiles') {
    dependsOn tasks.named('shadowJar'), tasks.named('createPom')
    doLast {
        def fromDir = file("${rootProject.projectDir}/mvn/com/boydti/fawe-bukkit/${rootProject.version}")
        def intoDir = file("${rootProject.projectDir}/mvn/com/boydti/fawe-bukkit/latest")
        copy {
            from fromDir
            into intoDir
            include('*.jar')
            rename("fawe-bukkit-${rootProject.version}.jar", 'fawe-bukkit-latest.jar')
        }
    }
}
