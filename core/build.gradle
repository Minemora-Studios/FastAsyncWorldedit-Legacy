plugins {
    id "java"
    id "java-library"
}

repositories {
    flatDir { dirs 'lib' }
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    implementation 'org.yaml:snakeyaml:1.16'
    implementation 'com.google.code.gson:gson:2.2.4'
    //implementation 'net.fabiozumbi12:redprotect:1.9.6'
    implementation 'com.plotsquared:plotsquared-api:3.1'
    implementation 'org.primesoft:BlocksHub:2.0'
    implementation 'com.github.luben:zstd-jni:1.1.1'
    implementation 'co.aikar:fastutil-lite:1.0'

    api('com.sk89q.worldedit:worldedit-core:6.1.4-SNAPSHOT') {
        exclude module: 'bukkit-classloader-check'
    }

    api('com.sk89q.worldedit:worldedit-bukkit:6.1.4-SNAPSHOT')

    implementation("org.graalvm.polyglot:polyglot:23.1.2")
    implementation("org.graalvm.polyglot:js-community:23.1.2@pom")

}

tasks.named('jar') {
    archiveFileName = "fawe-api-${rootProject.version}.jar"
    destinationDirectory = file("${rootProject.projectDir}/mvn/com/boydti/fawe-api/${rootProject.version}")
}

tasks.register('createPom') {
    doLast {
        def dirVer = file("${rootProject.projectDir}/mvn/com/boydti/fawe-api/${rootProject.version}")
        dirVer.mkdirs()
        def pomVer = """\
            <project>
              <modelVersion>4.0.0</modelVersion>
              <groupId>com.boydti</groupId>
              <artifactId>fawe-api</artifactId>
              <version>${rootProject.version}</version>
              <dependencies/>
            </project>
        """.stripIndent()
        file("${dirVer}/fawe-api-${rootProject.version}.pom").text = pomVer

        def dirLatest = file("${rootProject.projectDir}/mvn/com/boydti/fawe-api/latest")
        dirLatest.mkdirs()
        def pomLatest = """\
            <project>
              <modelVersion>4.0.0</modelVersion>
              <groupId>com.boydti</groupId>
              <artifactId>fawe-api</artifactId>
              <version>latest</version>
              <dependencies/>
            </project>
        """.stripIndent()
        file("${dirLatest}/fawe-api-latest.pom").text = pomLatest
    }
}

tasks.register('copyFiles') {
    dependsOn tasks.named('jar'), tasks.named('createPom')
    doLast {
        copy {
            from "${rootProject.projectDir}/mvn/com/boydti/fawe-api/${rootProject.version}/"
            into "${rootProject.projectDir}/mvn/com/boydti/fawe-api/latest/"
            include('*.jar')
            rename("fawe-api-${rootProject.version}.jar", 'fawe-api-latest.jar')
        }
    }
}

tasks.processResources {
    filteringCharset = 'UTF-8'
    filesMatching('fawe.properties') {
        expand(version: rootProject.version)
    }
}

tasks.named('build') {
    finalizedBy tasks.named('copyFiles')
}
